import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'java'
  id "com.github.hierynomus.license" version "0.14.0"
  id "com.xebialabs.xldp" version "1.0.5"
  id 'com.github.hierynomus.jython' version '0.8.0'
  id 'pl.allegro.tech.build.axion-release' version '1.9.2'
}

dependencies {
  jython ":onetimepass:1.0.1"
  jython ":certifi:2018.4.16"
  jython ":chardet:3.0.4"
  jython ":idna:2.7"
  jython ":requests:2.19.1"
  jython ":requests_toolbelt:0.8.0"
  jython ":urllib3:1.23"
}

license {
  header rootProject.file('License.md')
  ignoreFailures = true
  strictCheck = false
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'XEBIALABS'
}

scmVersion {
  tag {
    prefix = 'v'
    versionSeparator = ''
  }
}

project.version = scmVersion.version

processResources.configure {
  filter ReplaceTokens, tokens: [
    'project.version': version.toString(),
    'project.name'   : rootProject.name
  ]
}

project.tasks.release.dependsOn(project.tasks.build)

